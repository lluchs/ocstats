set terminal svg size 600,960
set output 'knüppeln-kd.svg'

set multiplot layout 2, 1

set style data histogram
set style fill solid

set colorsequence podo
set xtics rotate by -45
set grid ytics
set yrange [0:]
set title 'Knüppeln Kills and Deaths'

plot newhistogram 'overall', \
         '< sqlite3 -separator " " statistics.sqlite < knüppeln-kd.sql' using 2:xticlabels(1) title 'kills', \
         '' using 3:xticlabels(1) title 'deaths', \
     newhistogram 'normalized by selection frequency', \
	      '' using 4:xticlabels(1) title 'normalized kills', \
          '' using 5:xticlabels(1) title 'normalized deaths'

unset yrange
set xzeroaxis linetype 1
set title 'K/D Ratio'
unset key
set grid xtics
set boxwidth 0.2
plot '' using 0:(column(4) / column(5)):($2 < 1 ? 2 : 4):xticlabels(1) with boxes lc variable title 'kills - deaths'
